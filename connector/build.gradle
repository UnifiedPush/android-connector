plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdk 35

    compileOptions{
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        // languageVersion provides source compatibility with the specified version of Kotlin
        languageVersion = '1.7'
    }

    defaultConfig {
        minSdk 16
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig = false
    }

    namespace "org.unifiedpush.android.connector"
}

dependencies {
    if (project.plugins.hasPlugin('org.jetbrains.dokka')) {
        dokkaHtmlPlugin "org.jetbrains.dokka:android-documentation-plugin:$dokka_version"
        dokkaHtmlPartialPlugin "org.jetbrains.dokka:android-documentation-plugin:$dokka_version"
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // We use a local version of apps-webpush until they fix the decryption:
    // https://github.com/tink-crypto/tink-java-apps/pull/5
    implementation "com.google.crypto.tink:tink:1.17.0" // Remove once merged
    // implementation "com.google.crypto.tink:apps-webpush:1.11.0" // Uncomment once merged
    compileOnly 'androidx.annotation:annotation-jvm:1.9.1'
}

def releaseGroupId = 'org.unifiedpush.android'
def releaseArtifactId = 'connector'
def releaseVersion = '3.0.10'

tasks.register('printArtifactId') {
    group 'Artifact Info'
    doLast {
        println("artifact=$releaseGroupId:$releaseArtifactId")
    }
}

tasks.register("printVersion") {
    group 'Artifact Info'
    doLast {
        println("version=$releaseVersion")
    }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                pom {
                    name = "$releaseGroupId:$releaseArtifactId"
                    description = 'UnifiedPush connector library'
                    url = 'https://unifiedpush.org/developers/android/'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'S1m'
                            email = 'git@sgougeon.fr'
                            organization = 'UnifiedPush'
                            organizationUrl = 'https://unifiedpush.org/'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://codeberg.org/UnifiedPush/android-connector.git'
                        developerConnection = 'scm:git:ssh://codeberg.org:UnifiedPush/android-connector.git'
                        url = 'https://codeberg.org/UnifiedPush/android-connector'
                    }
                }
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = releaseGroupId
                artifactId = releaseArtifactId
                version = releaseVersion
            }
        }
    }
}
