plugins {
    id 'com.android.library'
    id 'kotlin-android'

    // for jitpack
    id 'maven-publish'
}

android {
    compileSdk 34

    compileOptions{
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        // languageVersion provides source compatibility with the specified version of Kotlin
        languageVersion = '1.7'
    }

    defaultConfig {
        minSdk 16
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig = false
    }

    namespace "org.unifiedpush.android.connector"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

def releaseGroupId = 'org.unifiedpush.android'
def releaseArtifactId = 'connector'
def releaseVersion = '2.4.0'

tasks.register('printArtifactId') {
    group 'Artifact Info'
    doLast {
        println("artifact=$releaseGroupId:$releaseArtifactId")
    }
}

tasks.register("printVersion") {
    group 'Artifact Info'
    doLast {
        println("version=$releaseVersion")
    }
}

// jitpack build
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = releaseGroupId
                artifactId = releaseArtifactId
                version = releaseVersion
            }
        }
    }
}
