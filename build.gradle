// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '2.0.10'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'org.jetbrains.dokka' version '1.9.20'
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

// DOKKA
import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.dokka.gradle.DokkaTaskPartial

def setDokkaBase(task) {
    String dokkaBaseConfiguration = """
    {
      "customStyleSheets": ["${file("$rootDir/dokka/tab_style.css")}"],
      "separateInheritedMembers": true,
      "mergeImplicitExpectActualDeclarations": false
    }
    """
    task.pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": dokkaBaseConfiguration]
    )
}

def setDokkaTask(task) {
    setDokkaBase(task)
    task.dokkaSourceSets.configureEach {
        if (project.file("Module.md").exists()) {
            includes.from(project.file("Module.md"))
        }
    }
}

subprojects {
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'kotlin-android'
    tasks.withType(DokkaTaskPartial.class) {
        setDokkaTask(it)
    }
    tasks.withType(DokkaTask.class) {
        setDokkaTask(it)
        outputDirectory.set(file("$rootDir/kdoc"))
    }
}

dokkaHtmlMultiModule {
    outputDirectory.set(file("$rootDir/kdoc"))
    moduleName.set("UnifiedPush")
    includes.from(project.file("$rootDir/Doc.md"))
    setDokkaBase(it)
}
